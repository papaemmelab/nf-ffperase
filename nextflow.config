// Metrics Files
dag {
    enabled = true
    file = 'pipeline_dag.html'
    overwrite = true
}
trace {
    enabled = true
    file = 'pipeline_trace.txt'
    fields = 'task_id,name,status,exit,realtime,%cpu,rss'
    overwrite = true
}
report {
    enabled = true
    file = 'pipeline_report.html'
    overwrite = true
}

// Default Params
params {
    version             = false
    help                = false
    step                = "full"
    vcf                 = null
    bam                 = null
    reference           = null
    features            = null
    model               = null
    modelName           = null
    bed                 = "${projectDir}/assets/gr37.no_mt_unmapped.bed.gz"
    picard              = "${projectDir}/assets/picard.jar"
    picardMetrics       = null
    minMapq             = 0
    minBaseq            = 0
    minDepth            = 0
    splitReads          = 7500000
    splitPileup         = 1000
    coverage            = null
    medianInsert        = null
    mutationType        = "snvs"
    tsv                 = "${projectDir}/assets/NO_FILE"
    outdir              = "${projectDir}/results"
}

params.outdirClassify   = "${params.outdir}/classify"
params.outdirPreprocess = "${params.outdir}/preprocess"
params.outdirTrain      = "${params.outdir}/train"
params.input            = "${params.outdirPreprocess}/features.tsv"

process {
    cache = "lenient"
    stageInMode = "symlink"
    queueSize = 200
}
executor.jobName = { "nf-ffperase-${params.mutationType}-${task.name}_${task.hash}" }

// Profiles
profiles {
    cloud {
        singularity.enabled = false
        docker {
          enabled = true
          runOptions = "--entrypoint ''"
        }
        process.container = "papaemmelab/nf-ffperase:v1.0.0"
    }

    hpc_slurm {
        singularity {
            enabled = true
            autoCleanUp = true
            autoMounts = true
            cacheDir = "/data1/papaemme/isabl/opt/singularity"
            envWhitelist = "SINGULARITY_BINDPATH,SINGULARITYENV_LD_LIBRARY_PATH,SINGULARITYENV_LD_PRELOAD"
        }
        process {
            container = "/usersoftware/papaemme/isabl/local/nf-ffperase/v1.0.0/nf_ffperase_v1.0.0.sif"

            withName: "split_pileup|merge_pileup|merge_picard" {
                executor = "local"
            }

            withName: "pileup|picard|split_intervals|annotate_variants|classify_random_forest" {
                array = 100
                executor = "slurm"
                queue = "preemptable"
                memory = { 4.GB * task.attempt }
                errorStrategy = { task.exitStatus in [137,139,140,143] ? "retry" : "terminate" }
                maxRetries = 3
            }

            withName: "annotate_variants|classify_random_forest" {
                memory = "16.GB"
            }
            withName: "classify_random_forest" {
                container = null
            }
        }
    }

    test { includeConfig 'tests/nextflow.config'}
}
